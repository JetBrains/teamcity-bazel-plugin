plugins {
    alias libs.plugins.teamcity.server
}

teamcity {
    version = libs.versions.teamcity.get()

    server {
        descriptor {
            name = "bazel"
            displayName = "Bazel build support"
            description = "Provides build facilities for bazel projects"
            downloadUrl = "https://github.com/JetBrains/teamcity-bazel-plugin"
            version = project.version.toString()
            vendorName = "JetBrains"
            vendorUrl = "https://www.jetbrains.com/"
            useSeparateClassloader = true
            allowRuntimeReload = true
            minimumBuild = "40000"
        }
        archiveName = 'teamcity-bazel-plugin'
        files {
            into('kotlin-dsl') {
                from("${rootProject.projectDir}/kotlin-dsl")
            }
        }
    }
}

dependencies {
    implementation project(':plugin-bazel-common')
    implementation (project(':bazel-build')) {
        exclude group: 'com.ibm.icu'
    }
    implementation libs.kotlin.stdlibjdk8
    compileOnly libs.jstl
    compileOnly libs.teamcity.internal.server
    agent project(path: ':plugin-bazel-agent', configuration: 'plugin')

    constraints {
        implementation(libs.constraint.transitive.icu4j) {
            because 'previous versions have faulty jar files which cause problems during incremental compilation (which is enabled by default since Kotlin 1.8.20)'
        }
    }
}