plugins {
    alias libs.plugins.kotlin
    alias libs.plugins.spotless
}

ext {
    def correctVersion = project.hasProperty('version') && property('version') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = correctVersion ? property('version') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
}

allprojects {
    group = "teamcity-bazel-plugin"
    version = versionNumber

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "com.diffplug.spotless"

    compileKotlin {
        kotlinOptions.allWarningsAsErrors = true
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.allWarningsAsErrors = true
        kotlinOptions.jvmTarget = "1.8"
    }

    tasks.withType(Test) {
        useTestNG()
    }

    java {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    spotless {
        kotlin {
            target("**/*.kt")
            ktlint(libs.versions.ktlint.get())
        }
    }
}
